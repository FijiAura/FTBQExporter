buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.1.0'
        classpath 'com.wynprice.cursemaven:CurseMaven:2.1.+'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven'
apply plugin: 'com.wynprice.cursemaven'

apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/markdown-git-changelog.gradle'

def ENV = System.getenv()
version = "forge-1.12.2-2.0.0"
group = package_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"
    mappings = "${mappings_version}"

    replace "0.0.0.forge", min_forge_version

    replace "0.0.0.ftblib", ftblib_version
    replace "0.0.0.ftbq", ftbquests_version
    replace "0.0.0.filters", filters_version

    replace "0.0.0.ftbqi", project.version

    replaceIn "${mod_reference}"
}

configurations.all {
    transitive = false
}

repositories {
    mavenLocal()
    maven { url "https://maven.latmod.com/" }

    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
}

dependencies {
    deobfCompile "com.feed_the_beast.mods:FTBLib:${ftblib_version}"
    //deobfCompile "com.feed_the_beast.mods:FTBUtilities:5.+"
    deobfCompile "com.feed_the_beast.mods:FTBQuests:${ftbquests_version}"
    deobfCompile "com.latmod.mods:ItemFilters:${filters_version}"

    // at runtime, use the full JEI jar
    runtime "mezz.jei:jei_${mc_version}:4.15.0.293"

    // test mods for dev env
    runtime "curse.maven:ctm:2915363"
    runtime "curse.maven:chisel:2915375"

    runtime "curse.maven:ae2:2747063"

    runtime "curse.maven:gtce:3017373"
    runtime "curse.maven:ccl:2779848"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

artifacts {
    archives sourceJar
}

if (ENV.LOCAL_MAVEN) {
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "file://${ENV.LOCAL_MAVEN}")
            }
        }
    }
}

if (ENV.CURSEFORGE_KEY) {
    curseforge {
        apiKey = ENV.CURSEFORGE_KEY
        project {
            id = project.curseforge_id
            releaseType = "release"
            addGameVersion project.mc_version
            relations {
                requiredDependency 'ftblib'
                requiredDependency 'ftb-quests'
            }
            changelog = getGitChangelog
            changelogType = 'markdown'
            addArtifact(sourceJar)
        }
    }
}